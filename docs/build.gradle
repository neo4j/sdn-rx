plugins {
    id 'org.asciidoctor.convert' version '1.5.8.1'
    id 'com.github.jruby-gradle.base' version '1.6.0'
    id 'org.kordamp.gradle.livereload' version '0.2.1'
}

project.buildDir = file('../target/docs')
jrubyPrepare.outputDir = file('../target/gems')

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.5.10'
}

apply plugin: 'java'
apply plugin: 'org.kordamp.gradle.livereload'

version = '1.0.0-SNAPSHOT'

asciidoctorj {
    version = '1.5.5'
}

asciidoctor {
    dependsOn jrubyPrepare
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepare.outputDir
    
    attributes \
        'build-gradle': file('build.gradle'),
        'endpoint-url': 'http://example.org',
        'source-highlighter': 'coderay',
        'imagesdir': 'images',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'idprefix': '',
        'idseparator': '-',
        'docinfo1': ''
    
    
    sourceDir = file('.')
    sources { include 'index.adoc', 'license.adoc' }

    resources {
        from(sourceDir) {
            include '**/*.png', '**/*.gif'
        }
    }
}

liveReload {
    docRoot asciidoctor.outputDir.canonicalPath
}
